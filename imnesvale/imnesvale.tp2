BACKUP ~imnesvale/backup~
//AUTHOR ~Duality (Dualities.Shadow@gmail.com)~
SUPPORT ~Spellhold Studios at http://www.shsforums.net/topic/32419-ds-odd-quest-mod/~

VERSION ~v2.1~

README ~imnesvale/readme/imnesvale-readme.txt~


ALWAYS

	ACTION_IF NOT VARIABLE_IS_SET bg2_chapter BEGIN // Always block must run only once per install!

		// Determines which game is installed and if the game is an Enhanced Edition.
		OUTER_SET is_eet = (GAME_IS "eet") ? 1 : 0						// eet

		/* eet compatibility */
		ACTION_IF (is_eet) BEGIN
			OUTER_SET bg2_chapter = 12
		END ELSE BEGIN
			OUTER_SET bg2_chapter = 0
		END
		OUTER_FOR (i = 1 ; i <= 10 ; ++i) BEGIN
			OUTER_SET bg2_chapter = bg2_chapter + 1
			OUTER_SPRINT name_source ~bg2_chapter_%i%~
			OUTER_SET EVAL ~%name_source%~ = bg2_chapter
		END

		/* ---------------------------------------------------------------------------------------------- *
		 *    Convert strings to UTF-8 for BGEE/BG2EE                                                     *
		 *    No exception, everything needs to be converted                                              *
		 * -----------------------------------------------------------------------*---------------------- */
		ACTION_DEFINE_ARRAY du#noconvert BEGIN END
		// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
		ACTION_DEFINE_ARRAY du#reload BEGIN setup misc END

		LAF HANDLE_CHARSETS
			INT_VAR
				infer_charsets = 1
			STR_VAR
				tra_path = EVAL ~%MOD_FOLDER%/lang~
				noconvert_array = du#noconvert
				reload_array = du#reload
		END

	END

END


AUTO_TRA ~imnesvale/lang/%s~

LANGUAGE ~English~
         ~english~
		 ~imnesvale/lang/english/setup.tra~
		 ~imnesvale/lang/english/misc.tra~

LANGUAGE ~Deutsch (Uebersetzung von BiG World Textpack German project)~
         ~german~
		 ~imnesvale/lang/german/setup.tra~
		 ~imnesvale/lang/german/misc.tra~

LANGUAGE ~Russian (translation by prowler)~
         ~russian~
		 ~imnesvale/lang/russian/setup.tra~
		 ~imnesvale/lang/russian/misc.tra~

LANGUAGE ~Italiano (traduzione di ilot)~
         ~italian~
		 ~imnesvale/lang/italian/setup.tra~
		 ~imnesvale/lang/italian/misc.tra~


/* ====================== *
 *  Imnesvale Mod : Easy  *
 * ====================== */
BEGIN @1											// ~Easy~
SUBCOMPONENT @0 LABEL ~imnesvale_easy~				// ~Install Imnesvale Mod~
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @4	// ~You must have ToB, BGT, BG2:EE or EET installed to install this mod.~

OUTER_SPRINT difficulty "easy"
INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


/* ====================== *
 *  Imnesvale Mod : Hard  *
 * ====================== */
BEGIN @2			// ~Hard~
SUBCOMPONENT @0 LABEL ~imnesvale_hard~
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @4

OUTER_SPRINT difficulty "hard"
INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


/* ===================================================== *
 *  Imnesvale Mod : Are You Out Of Your Freaking Mind?!  *
 * ===================================================== */
BEGIN @3			// ~Are You Out Of Your Freaking Mind?!~
SUBCOMPONENT @0 LABEL ~imnesvale_impossible~
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @4

OUTER_SPRINT difficulty "impossible"
INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~
